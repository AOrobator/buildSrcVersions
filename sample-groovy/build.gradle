plugins {
    id 'java'
    id 'application'
    id 'idea'
    id "io.vertx.vertx-plugin" version "0.3.1"
    id 'de.fayard.buildSrcVersions' version '0.5.0'
}

group = "de.fayard"

repositories {
    maven {
        setUrl("../plugin/src/test/resources/maven")
    }
    mavenCentral()
}

dependencies {
    implementation("com.google.guava:guava:15.0")
    implementation("com.google.inject:guice:2.0")
}

vertx {
    mainVerticle = 'sample.App'
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
}

buildSrcVersions {
    useFqdnFor('module')
    renameLibs = "Libs"
    renameVersions = "Versions"
    indent = "  "
    rejectVersionIf {
        isNonStable(candidate.version) && !isNonStable(currentVersion)
    }
}
wrapper {
    gradleVersion = System.getenv("GRADLE_VERSION") ?: "5.6.1"
    distributionType = Wrapper.DistributionType.ALL
}
